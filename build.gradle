/*
 * Copyright (c) 2015, The MITRE Corporation. All rights reserved.
 * See LICENSE for complete terms.
 *
 * Java-STIX Gradle Buildscript
 *
 * nemonik (Michael Joseph Walsh <github.com@nemonik.com>)
 *
 * Run
 *
 *	./gradlew -x signArchives
 *
 * from the commnand-line and the build task will execute by default.
 *
 * The Jar will be built to 
 *
 * 	build/libs/stix-${version}.jar
 * 
 *
 * Publishing archives:
 *
 * signingKeyId, signingPassword, secretKeyFile, ossrhUsername, and ossrhPassword properties
 * should be defined outside of the repository either on the command-line via
 * 
 * ./gradlew -PsigningKeyId=value -PsigningPassword=value -PsecretKeyFile=value -PossrhUsername=value -PossrhPassword=value
 *
 * or via ~/.gradle/gradle.properties as 
 *
 * signingKeyId=value
 * signingPassword=value
 * secretKeyFile=value
 * ossrhUsername=value
 * ossrhPassword=value
 *
 * Check settings via:
 *
 * To publish a snapshot execute the "uploadArchives" task like so:
 *
 * ./gradlew uploadArchives
 *
 * Otherwise you can publish a release to staging by executing the "uploadArchives" task 
 * with "release" property like so:
 *
 * ./gradlew uploadArchives -Prelease
 *
 * ./gradle checkProperties
 *
 */

import org.gradle.plugins.signing.Sign

import org.mitre.stix.RetrieveSchemasTask
import org.mitre.stix.PrefixNamespaceBindingsTask
import org.mitre.stix.GenerateJAXBTask
import org.mitre.stix.CreatePackageInfoTask
import org.mitre.stix.GeneratedSourceTransformationTask
import org.mitre.stix.CleanSchemaTask
import org.mitre.stix.CalcChecksumTask

apply plugin: "java"
apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "maven"
apply plugin: "signing"

defaultTasks "build"

if (!has("release")) {
	version = "${version}-SNAPSHOT"
}

task retrieveSchemas(type: RetrieveSchemasTask, dependsOn: clean) {
	schemaVersion = project.schemaVersion
}

task createPrefixNamespaceBindings(type: PrefixNamespaceBindingsTask, dependsOn: retrieveSchemas) {
	schemaVersion = project.schemaVersion
}

task generateJAXB(type: GenerateJAXBTask, dependsOn: createPrefixNamespaceBindings) {
	classpath= buildscript.configurations.classpath
	schemaVersion = project.schemaVersion
}

task generatedSourceTransformation(type: GeneratedSourceTransformationTask, dependsOn: generateJAXB)

task createPackageInfo(type: CreatePackageInfoTask, dependsOn: generatedSourceTransformation) {
	schemaVersion = project.schemaVersion
}
task cleanSchema(type: CleanSchemaTask) {
	schemaVersion = project.schemaVersion
}

task calcChecksum(type: CalcChecksumTask) {
	schemaVersion = project.schemaVersion
}

if (!has("skipPackageInfo")) {
	compileJava.dependsOn createPackageInfo
}

task wrapper(type: Wrapper) {
	gradleVersion = '2.4'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from 'LICENSE'
	from javadoc
}

task sourcesJar(type: Jar, dependsOn: javadoc) {
	classifier = 'sources'
	from 'LICENSE'
	from 'README.md'
	from sourceSets.main.allSource
}

task checkProperties() {
	doFirst {
		println "${project.hasProperty("signingKeyId") ? 'signingKeyId = ' + signingKeyId : 'No value has been specified for property \'signingKeyId\'.'}" 
		println "${project.hasProperty("signingPassword") ? 'signingPassword = ' + signingPassword : 'No value has been specified for property \'signingKeyId\'.'}"
		println "${project.hasProperty("secretKeyFile") ? 'secretKeyFile = ' + secretKeyFile : 'No value has been specified for property \'secretKeyFile\'.'}"
		println "${project.hasProperty("ossrhUsername") ? 'ossrhUsername = ' + ossrhUsername : 'No value has been specified for property \'ossrhUsername\'.'}"
		println "${project.hasProperty("ossrhPassword") ?'ossrhPassword = ' +  ossrhPassword : 'No value has been specified for property \'ossrhPassword\'.'}"
	}
}

task execute(type:JavaExec) {
	main = project.mainClass
	classpath = sourceSets.main.runtimeClasspath
}

task gatherDependencies(type: Copy) {
	into "${buildDir}/dependencies"
	from configurations.runtime
}

uploadArchives{
	configuration = configurations.archives
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment ->
				if (!project.hasProperty("ossrhUsername")) {
					throw new GradleException("No value has been specified for property 'ossrhUsername'.")
				}
				
				if (!project.hasProperty("ossrhPassword")) {
					throw new GradleException("No value has been specified for property 'ossrhPassword'.")
				}
		
				signing.signPom(deployment)
			}
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: project.hasProperty("ossrhUsername") ? ossrhUsername : "", password: project.hasProperty("ossrhPassword") ? ossrhPassword: "")
			}
			
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: project.hasProperty("ossrhUsername") ? ossrhUsername : "", password: project.hasProperty("ossrhPassword") ? ossrhPassword: "")
			}
			
			pom.project {
				name "${project.name}"
				packaging 'jar'
				version "${project.version}"
				// optionally artifactId can be defined here 
				description "The Java bindings for STIX v." + version
				url "https://github.com/STIXProject/java-stix"
				
				scm {
					connection "https://github.com/STIXProject/java-stix.git"
					developerConnection "https://github.com/STIXProject/java-stix.git"
					url "https://github.com/STIXProject/java-stix"
				}
				
				licenses {
					license {
						name "The BSD 3-Clause License"
						url "https://raw.githubusercontent.com/STIXProject/java-stix/master/LICENSE"
					}
				}
				
				developers {
					developer {
						id 'nemonik'
						name 'Michael Joseph Walsh'
						email 'java-stix-pom[ASCII character number 64]nemonik.com'
					}
				}
			}
		}
	}
}

gradle.taskGraph.whenReady { taskGraph ->
	if (taskGraph.allTasks.any { it instanceof Sign}) {
		allprojects {
			ext."signing.keyId" = project.hasProperty("signingKeyId") ? signingKeyId : ""
			ext."signing.password" = project.hasProperty("signingPassword") ? signingPassword: ""
			ext."signing.secretKeyRingFile" = project.hasProperty("secretKeyFile") ? secretKeyFile: ""
		}
	}
		
	if (taskGraph.hasTask(tasks.uploadArchives)) {
		if (has("release")) {
			if (!project.hasProperty("signingKeyId")) {
				throw new GradleException("No value has been specified for property 'signingKeyId'.")
			}
			
			if (!project.hasProperty("signingPassword")) {
				throw new GradleException("No value has been specified for property 'signingPassword'.")
			}
			
			if (!project.hasProperty("secretKeyFile")) {
				throw new GradleException("No value has been specified for property 'secretKeyFile'.")
			}
		}
	}
}

signing {
	required {has("release") && gradle.taskGraph.hasTask(tasks.uploadArchives) }
	sign configurations.archives
}

clean {
	delete("src/generated/java")
	delete("src/main/resources/namespace-prefix.xjb")
}

sourceSets {
	main {
		java { srcDirs("src/main/java", "src/generated/java") }
		resources {
			srcDir("src/main/resources")
			exclude "*.xjb"
			exclude "**/v1.2-rc1"
		}
	}
}

repositories {
	jcenter()
	mavenCentral()
	mavenLocal()
	flatDir { dirs "libs" }
}

dependencies {
	compile "org.jvnet.jaxb2_commons:jaxb2-basics-runtime:0.6.5"
	compile "org.apache.httpcomponents:httpclient:4.3.5"
	compile "org.apache.httpcomponents:httpclient-cache:4.3.5"
	compile "org.apache.httpcomponents:httpmime:4.3.5"
	compile "org.apache.httpcomponents:fluent-hc:4.3.5"
	
	compile "org.springframework:spring-core:4.1.4.RELEASE"
	
	compile "commons-io:commons-io:2.4"
	
	compile "commons-lang:commons-lang:2.6"
	
	testCompile "org.codehaus.groovy:groovy:2.4.3"
	testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
	testCompile "xmlunit:xmlunit:1.6"
}

jar {
	manifest {
		attributes 'Specification-Title': 'Structured Threat Information eXpression (STIX)',
		'Specification-Version': version,
		'Implementation-Title': 'Java-STIX',
		'Implementation-Version': version,
		'Implementation-Vendor': 'The MITRE Corporation'
	}

	from 'LICENSE'
}

javadoc {
	include "**/org/mitre/stix/**/*"
	include "**/com/mandiant/**/*"
	include "**/gov/nist/**/*"
	include "**/oasis/nist/**/*"
	include "**/org/icasi/**/*"
	include "**/org/mitre/**/*"
	include "**/org/purl/**/*"
	include "**/org/w3/**/*"
	
	options.overview = "src/main/javadoc/overview.html";
	options.showAll()
	options.encoding("UTF-8")
	options.setUse(true)
	options.author(true)
	options.version(true)
	options.windowTitle("java-stix v." + version)
	options.docTitle("java-stix v." + version)
	options.footer("Copyright (c) 2015, The MITRE Corporation. All rights reserved.")
	
	doFirst {
		javadoc.title = "java-stix v." + version
		javadoc.options.docTitle = javadoc.title
	}
}

if (JavaVersion.current().isJava8Compatible()) {
	tasks.withType(Javadoc) {
		// disable strict doclint in Java 8
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

artifacts {
	archives jar
	archives sourcesJar
	archives javadocJar
}

buildscript {
	repositories {
		jcenter()
		mavenCentral()
		mavenLocal()
		
		flatDir { dirs "libs" }
	}

	dependencies {
		// Needed for GeneratedSourceTransformationTask
		classpath name: "org.eclipse.core.runtime_3.10.0.v20140318-2214"
		classpath name: "org.eclipse.osgi_3.10.0.v20140606-1445"
		classpath name: "org.eclipse.core.contenttype_3.4.200.v20140207-1251"
		classpath name: "org.eclipse.equinox.common_3.6.200.v20130402-1505"
		classpath name: "org.eclipse.text_3.5.300.v20130515-1451"
		classpath name: "org.eclipse.core.jobs_3.6.0.v20140424-0053"
		classpath name: "org.eclipse.equinox.preferences_3.5.200.v20140224-1527"
		classpath name: "org.eclipse.core.resources_3.9.0.v20140514-1307"
		classpath name: "org.eclipse.jdt.core_3.10.0.v20140604-1726"
		classpath name: "org.eclipse.equinox.registry_3.5.400.v20140428-1507"
		
		// Needed for GenerateJAXBTask
		classpath "com.sun.xml.bind:jaxb-xjc:2.2.5-2"
		classpath "com.sun.xml.bind:jaxb-impl:2.2.5-2"
		classpath "javax.xml.bind:jaxb-api:2.2.+"
		
		classpath "org.jvnet.jaxb2_commons:jaxb2-basics:0.6.5"
		classpath "org.jvnet.jaxb2_commons:jaxb2-basics-ant:0.6.5"
		classpath "org.jvnet.jaxb2_commons:jaxb2-fluent-api:3.0"
		classpath "org.jvnet.jaxb2_commons:jaxb2-value-constructor:3.0"	
		classpath "org.jvnet.jaxb2_commons:jaxb2-default-value:1.1"
		classpath "org.jvnet.jaxb2_commons:jaxb2-namespace-prefix:1.1"
	}
}
